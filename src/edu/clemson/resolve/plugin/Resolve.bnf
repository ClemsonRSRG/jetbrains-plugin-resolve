{
    parserClass='edu.clemson.resolve.plugin.parser.ResParser'

    implements='edu.clemson.resolve.plugin.psi.ResCompositeElement'
    extends='edu.clemson.resolve.plugin.psi.impl.ResCompositeElementImpl'

    elementTypeHolderClass='edu.clemson.resolve.plugin.ResTypes'
    elementTypeClass='edu.clemson.resolve.plugin.psi.ResCompositeElementType'
    tokenTypeClass='edu.clemson.resolve.plugin.psi.ResTokenType'

    psiClassPrefix='Res'
    psiImplClassSuffix='Impl'
    psiPackage='edu.clemson.resolve.plugin.psi'
    psiImplPackage='edu.clemson.resolve.plugin.psi.impl'
    psiImplUtilClass="edu.clemson.resolve.plugin.psi.impl.ResPsiImplUtil"

    tokens=[
        // Punctuation
        AT                      = '@'
        TRIPLE_DOT              = '...'
        DOT_DOT                 = '..'
        DOT                     = '.'
        DBL_LBRACE              = '{{'
        LBRACE                  = '{'
        RBRACE                  = '}'
        DBL_RBRACE              = '}}'
        LBRACK                  = '['
        RBRACK                  = ']'
        LPAREN                  = '('
        RPAREN                  = ')'
        COLON                   = ':'
        COLONCOLON              = '::'
        SEMICOLON               = ';'
        COMMA                   = ','

        // Operators
        EQUALS                  = '='
        NEQUALS                 = '/='

        AND                     = 'and'
        OR                      = 'or'
        NOT                     = 'not'
        CAT                     = 'o'

        TRUE                    = 'true'
        FALSE                   = 'false'

        LESS_OR_EQUAL           = '<='
        LESS                    = '<'
        GREATER_OR_EQUAL        = '>='
        GREATER                 = '>'

        MOD                     = '%'
        MUL                     = '*'
        QUOTIENT                = '/'
        PLUS_PLUS               = '++'
        PLUS                    = '+'
        MINUS_MINUS             = '--'
        MINUS                   = '-'

        COLON_EQUALS            = ':='
        COLON_EQUALS_COLON      = ':=:'
        RARROW                  = '->'
        TILDE                   = '~'
        UNION                   = 'union'
        INTERSECT               = 'intersect'
        IS_IN                   = 'is_in'
        IS_NOT_IN               = 'is_not_in'
        BAR                     = '|'
        DBL_BAR                 = '||'

        // Keywords
        INT                     = 'int'
        FACILITY                = 'Facility'
        PRECIS                  = 'Precis'
        BASE                    = 'base'
        HYPO                    = 'hypo'
        CONCEPT                 = 'Concept'
        CART_PROD               = 'Cart_Prod'
        Definition              = 'Definition'
        CONSTRAINTS             = 'constraints'
        CONVENTIONS             = 'conventions'
        CORRESPONDENCE          = 'correspondence'
        EXTENSION               = 'extension'
        END                     = 'end'
        USES                    = 'uses'
        IS                      = 'is'
        IF                      = 'if'
        PROG_IF                 = 'If'
        CHANGING                = 'changing'
        MAINTAINING             = 'maintaining'
        DECREASING              = 'decreasing'
        OTHERWISE               = 'otherwise'
        FORALL                  = 'Forall'
        EXISTS                  = 'Exists'
        EXTERNALLY              = 'externally'
        IMPLEMENTED             = 'implemented'
        INITIALIZATION          = 'initialization'
        IMPLEMENTATION          = 'Implementation'
        INDUCTIVE               = 'Inductive'
        LAMBDA                  = 'lambda'
        FAMILY_TYPE             = 'Type'
        PARAM_TYPE              = 'type'
        FAMILY                  = 'Family'
        FOR                     = 'for'
        MODELED                 = 'modeled'
        EXEMPLAR                = 'exemplar'
        OPERATION               = 'Operation'
        RECURSIVE               = 'Recursive'
        RECORD                  = 'Record'
        REQUIRES                = 'requires'
        ENSURES                 = 'ensures'
        PROCEDURE               = 'Procedure'
        VAR                     = 'Var'
        BY                      = 'by'
        THEN                    = 'then'
        WHILE                   = 'While'
        DO                      = 'do'
        STRING                  = 'string'
        WHICH_ENTAILS           = 'which_entails'
        CHAR                    = 'char'
        raw_string              = 'raw_string'

        // Parameter modes
        ALTERS                  = 'alters'
        UPDATES                 = 'updates'
        CLEARS                  = 'clears'
        RESTORES                = 'restores'
        PRESERVES               = 'preserves'
        REPLACES                = 'replaces'
        EVALUATES               = 'evaluates'

        //these two are for grammarkit's live preview feature work right,
        //disregard them otherwise
        space                   = "regexp:\s+"
        identifier              = 'regexp:\p{Alpha}\w*'
    ]

    extends("(Facility|Concept)ModuleDecl")="edu.clemson.resolve.plugin.psi.impl.ResAbstractModuleDeclImpl"
    extends("(OperationProcedure)Decl")="edu.clemson.resolve.plugin.psi.impl.ResOperationLikeNodeImpl"
    extends("(Facility|Operation)Decl|(Uses)Spec|(Param|Var|Field)Def")="edu.clemson.resolve.plugin.psi.impl.ResNamedElementImpl"
    extends("(TypeModel|TypeRepr)Decl")="edu.clemson.resolve.plugin.psi.impl.ResAbstractTypeLikeDeclImpl"
    extends("(Concept|Facility)Block")="edu.clemson.resolve.plugin.psi.impl.ResAbstractModuleBlockImpl"

    extends("(Or|Selector)Expr")=BinaryExpr
    extends(".*Statement")=Statement
    extends(".*(Expr)")=Expression

    implements("OperationDecl")="edu.clemson.resolve.plugin.psi.ResNamedSignatureOwner"
    implements(".*ReferenceExpression")="edu.clemson.resolve.plugin.psi.ResReferenceExpressionBase"
    implements("(TypeModel|TypeRepr)Decl")="edu.clemson.resolve.plugin.psi.ResTypeLikeNodeDecl"
    implements("(Facility|Concept)Block")="edu.clemson.resolve.plugin.psi.ResModuleBlock"
    implements("(OperationProcedure)Decl")="edu.clemson.resolve.plugin.psi.ResOperationWithBodyNode"
    implements("(Var|Param)Def|(Facility)Decl|(Uses)Spec|(Field)Def")="edu.clemson.resolve.plugin.psi.ResNamedElement"
    implements("(Facility|Concept)ModuleDecl")="edu.clemson.resolve.plugin.psi.ResModuleDecl"

    generateTokenAccessors=true
    pin('.*List(?:_\d.*)?')=1
}

ModuleDecl ::= FacilityModuleDecl | ConceptModuleDecl <<eof>>

FacilityModuleDecl ::= 'Facility' identifier ';'
UsesList?
FacilityBlock
'end' identifier ';' {pin(".*")=1}

ConceptModuleDecl ::= 'Concept' identifier SpecModuleParameters? ';'
UsesList?
ConceptBlock
'end' identifier ';' {pin(".*")=1}

UsesList ::= 'uses' UsesSpec (',' UsesSpec)* ';'

UsesSpec ::= identifier {methods=[getReferences resolve getUsesTextRange]}

FacilityBlock ::= FacilityItem* {methods=[processDeclarations] recoverWhile=ItemsRecover}

ConceptBlock ::= ConceptItem* {methods=[processDeclarations] recoverWhile=ItemsRecover}

private ItemsRecover ::= !('end')

private FacilityItem ::=
        OperationProcedureDecl
    |   FacilityDecl
    |   TypeReprDecl {pin=1 recoverWhile=FacilityItemRecover}

private FacilityItemRecover ::= !('Operation'|'Facility'|'Type'|'end')

private ConceptItem ::=
        TypeModelDecl
    |   OperationDecl {pin=1 recoverWhile=ConceptItemRecover}

private ConceptItemRecover ::= !('Type'|'Operation'|'end')

FacilityDecl ::= 'Facility' identifier 'is' UsesSpec
('externally')? 'implemented' 'by' UsesSpec ';' {pin=2 methods=[getSpecification]}

TypeModelDecl ::= 'Type' 'Family' identifier 'is' 'modeled' 'by' identifier ';'
    'exemplar' identifier ';' {pin=3}

TypeReprDecl ::= 'Type' identifier '=' (RecordType|Type) ';' {pin=2 methods=[getResTypeInner]}

OperationProcedureDecl ::= 'Operation' identifier Signature ';'
('Recursive')? 'Procedure'
Block
'end' identifier ';' {pin=1 implements="edu.clemson.resolve.plugin.psi.ResSignatureOwner"}

Signature ::= OperationLikeParameters Result? {pin=1}

Result ::= ':' Type {pin=2}

OperationDecl ::= 'Operation' identifier Signature ';' {pin=1}

SpecModuleParameters ::= '(' SpecModuleParamList ')' {pin=1 }

private SpecModuleParamList ::= SpecParamDecl  (';' SpecParamDecl)* {pin=2 recoverWhile=SpecListRec}

private SpecListRec ::= !(')')

SpecParamDecl ::= TypeParamDecl | ParamDecl

TypeParamDecl ::= 'type' identifier {pin=2}

OperationLikeParameters ::= '(' OpParamList? ')' { pin=1}
private ParamListRec ::= !(')')
private OpParamList ::= ParamDecl (';' ParamDecl)* {pin=1 recoverWhile=ParamListRec}
ParamDecl ::= ParameterMode ParamDefinitionListNoPin ':' Type {pin=1}
private ParamDefinitionListNoPin ::= ParamDef &(!(';'|')')) (',' ParamDef)* {pin=1}
ParamDef ::= identifier //{methods = [isVariadic]}

Block ::=
VarDeclGroupList
Statements

RecordVarDeclGroup ::= FieldVarSpec ';' {pin=1}
FieldVarSpec ::= FieldVarDefList ':' Type {pin=1}
private FieldVarDefList ::= FieldDef &(!(';'))(',' FieldDef)*
FieldDef ::= identifier

VarDeclGroupList ::= VarDeclGroup* {pin=1}

VarDeclGroup ::= 'Var' VarSpec ';' {pin=1}
VarSpec ::= VarDefList ':' Type {pin=1}//{methods=[processDeclarations]}
private VarDefList ::= VarDef &(!(';'))(',' VarDef)*
VarDef ::= identifier {methods=[getResTypeInner getReference]}

//TOOD: Really this should just be namedType (w/o records). Right now this
//allows anonymous record types .. which is ok .. I GUESS.
Type ::= RecordType | NamedType
private NamedType ::= TypeReferenceExpression QualifiedTypeReferenceExpression?
TypeReferenceExpression ::= identifier { methods=[getReference getQualifier] }
left QualifiedTypeReferenceExpression ::= '.' identifier {elementType=TypeReferenceExpression}

RecordType ::= 'Record' RecordVarDeclGroup* 'end' {extends=Type}

private Statements ::= Statement* {recoverWhile=StatementRecover}

Statement ::= SimpleStatement
private StatementRecover ::= !('end'|':='|':=:')
SimpleStatement ::=
      Expression (AssignStatement|SwapStatement)? ';' {pin(".*")=1}

left AssignStatement ::= ':=' Expression {pin=1}
left SwapStatement ::= ':=:' Expression {pin=1}

Expression ::=
      OrExpr
    | MaxGroup {methods=[getResType] implements="edu.clemson.resolve.plugin.psi.ResTypeOwner"}

fake BinaryExpr ::= Expression+ {
  methods=[left="Expression[0]" right="Expression[1]"]
}

OrExpr ::= Expression 'or' Expression

private MaxGroup ::=
      NameExpr
    | CallExpr
    | SelectorExpr

NameExpr ::= ReferenceExpression QualifiedReferenceExpression? {elementType=ReferenceExpression}

left QualifiedReferenceExpression ::= '.' identifier {elementType=ReferenceExpression}
ReferenceExpression ::= identifier { methods=[getReference getQualifier] extends=Expression}

CallExpr ::= NameExpr ArgumentList

ArgumentList ::= '(' ExpressionArgList? ')' {pin=1}
private ExpressionArgList ::= ExpressionWithRecover (',' ExpressionWithRecover)* {pin(".*")=1}
private ExpressionWithRecover ::= Expression {recoverWhile=ExpressionListRecover}
private ExpressionListRecover ::= !('%' | '+' | '-' | '(' | ')' | ',' | '*' | '++' | '<' | '<=' | '>' | '>=' | 'end' | 'Operation' | 'Type' | 'Facility')

SelectorExpr ::= Expression '.' Expression

ParameterMode ::=
      'alters'
    | 'updates'
    | 'clears'
    | 'restores'
    | 'preserves'
    | 'replaces'
    | 'evaluates'
    | identifier //TODO: would like to get rid of identifier here, but results
                 //in an uglier (partial) psi; which makes keyword completion
                 //patterns much harder