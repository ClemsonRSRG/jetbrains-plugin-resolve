{
    parserClass='edu.clemson.resolve.plugin.parser.ResParser'

    implements='edu.clemson.resolve.plugin.psi.ResCompositeElement'
    extends='edu.clemson.resolve.plugin.psi.impl.ResCompositeElementImpl'

    elementTypeHolderClass='edu.clemson.resolve.plugin.ResTypes'
    elementTypeClass='edu.clemson.resolve.plugin.psi.ResCompositeElementType'
    tokenTypeClass='edu.clemson.resolve.plugin.psi.ResTokenType'

    psiClassPrefix='Res'
    psiImplClassSuffix='Impl'
    psiPackage='edu.clemson.resolve.plugin.psi'
    psiImplPackage='edu.clemson.resolve.plugin.psi.impl'
    psiImplUtilClass="edu.clemson.resolve.plugin.psi.impl.ResPsiImplUtil"

    tokens=[
        // Punctuation
        AT                      = '@'
        TRIPLE_DOT              = '...'
        DOT_DOT                 = '..'
        DOT                     = '.'
        DBL_LBRACE              = '{{'
        LBRACE                  = '{'
        RBRACE                  = '}'
        DBL_RBRACE              = '}}'
        LBRACK                  = '['
        RBRACK                  = ']'
        LPAREN                  = '('
        RPAREN                  = ')'
        COLON                   = ':'
        COLONCOLON              = '::'
        SEMICOLON               = ';'
        COMMA                   = ','

        // Operators
        EQUALS                  = '='
        NEQUALS                 = '/='

        AND                     = 'and'
        OR                      = 'or'
        NOT                     = 'not'
        CAT                     = 'o'

        TRUE                    = 'true'
        FALSE                   = 'false'

        LESS_OR_EQUAL           = '<='
        LESS                    = '<'
        GREATER_OR_EQUAL        = '>='
        GREATER                 = '>'

        MOD                     = '%'
        MUL                     = '*'
        QUOTIENT                = '/'
        PLUS_PLUS               = '++'
        PLUS                    = '+'
        MINUS_MINUS             = '--'
        MINUS                   = '-'

        COLON_EQUALS            = ':='
        COLON_EQUALS_COLON      = ':=:'
        RARROW                  = '->'
        TILDE                   = '~'
        UNION                   = 'union'
        INTERSECT               = 'intersect'
        IS_IN                   = 'is_in'
        IS_NOT_IN               = 'is_not_in'
        BAR                     = '|'
        DBL_BAR                 = '||'

        // Keywords
        INT                     = 'int'
        FACILITY                = 'Facility'
        PRECIS                  = 'Precis'
        BASE                    = 'base'
        HYPO                    = 'hypo'
        CONCEPT                 = 'Concept'
        CART_PROD               = 'Cart_Prod'
        Definition              = 'Definition'
        CONSTRAINTS             = 'constraints'
        CONVENTIONS             = 'conventions'
        CORRESPONDENCE          = 'correspondence'
        EXTENSION               = 'extension'
        END                     = 'end'
        ELSE                    = 'else'
        USES                    = 'uses'
        IS                      = 'is'
        IF                      = 'if'
        PROG_IF                 = 'If'
        CHANGING                = 'changing'
        MAINTAINING             = 'maintaining'
        DECREASING              = 'decreasing'
        OTHERWISE               = 'otherwise'
        FORALL                  = 'Forall'
        EXISTS                  = 'Exists'
        EXTERNALLY              = 'externally'
        IMPLEMENTED             = 'implemented'
        INITIALIZATION          = 'initialization'
        IMPLEMENTATION          = 'Implementation'
        INDUCTIVE               = 'Inductive'
        LAMBDA                  = 'lambda'
        FAMILY_TYPE             = 'Type'
        PARAM_TYPE              = 'type'
        FAMILY                  = 'Family'
        FOR                     = 'for'
        MODELED                 = 'modeled'
        EXEMPLAR                = 'exemplar'
        OPERATION               = 'Operation'
        RECURSIVE               = 'Recursive'
        RECORD                  = 'Record'
        REQUIRES                = 'requires'
        ENSURES                 = 'ensures'
        PROCEDURE               = 'Procedure'
        VAR                     = 'Var'
        BY                      = 'by'
        THEN                    = 'then'
        WHILE                   = 'While'
        DO                      = 'do'
        STRING                  = 'string'
        WHICH_ENTAILS           = 'which_entails'
        CHAR                    = 'char'
        raw_string              = 'raw_string'

        // Parameter modes
        ALTERS                  = 'alters'
        UPDATES                 = 'updates'
        CLEARS                  = 'clears'
        RESTORES                = 'restores'
        PRESERVES               = 'preserves'
        REPLACES                = 'replaces'
        EVALUATES               = 'evaluates'

        //these two are for grammarkit's live preview feature work right,
        //disregard them otherwise
        space                   = "regexp:\s+"
        identifier              = 'regexp:\p{Alpha}\w*'
    ]

    extends("Math.*(DefinitionDecl)")="edu.clemson.resolve.plugin.psi.impl.ResAbstractMathDefinitionDeclImpl"
    extends("Math.*(Signature)")="edu.clemson.resolve.plugin.psi.impl.ResAbstractMathSignatureImpl"

    implements(".*ReferenceExp")="edu.clemson.resolve.plugin.psi.ResReferenceExpBase"
    implements("Math.*Signature")="edu.clemson.resolve.plugin.psi.ResMathDefinitionSignature"
    implements("Math.*DefinitionDecl")="edu.clemson.resolve.plugin.psi.ResMathDefinitionDecl"
    implements(".*Block")="edu.clemson.resolve.plugin.psi.ResBlock"

    extends("Math.*(Exp)")=MathExp

    generateTokenAccessors=true
    pin('.*List(?:_\d.*)?')=1
}

ModuleDecl ::= PrecisModuleDecl <<eof>>

PrecisModuleDecl ::= 'Precis' identifier ';'
UsesItemList?
PrecisBlock
'end' identifier ';' {pin(".*")=1}

UsesItemList ::= 'uses' UsesItem (',' UsesItem)* ';'

UsesItem ::= identifier {pin=1 methods=[getReferences resolve getUsesTextRange]}

PrecisBlock ::= PrecisItem* {methods=[processDeclarations] recoverWhile=ItemsRecover}

private ItemsRecover ::= !('end')

private PrecisItem ::=
        MathStandardDefinitionDecl  {pin=1 recoverWhile=PrecisItemRecover}

private PrecisItemRecover ::= !('Definition'|'end')

// -------------------------------------------------
//  M A T H   C O N S T R U C T S   &   E X P R S
// -------------------------------------------------

MathStandardDefinitionDecl ::= 'Definition' MathDefinitionSignature ';' {pin=2}

private MathDefinitionSignature ::=
           MathPrefixDefinitionSignature
        |  MathInfixDefinitionSignature

MathPrefixDefinitionSignature ::=
          identifier MathDefinitionParams? MathResult {pin=1}

MathInfixDefinitionSignature ::=
        '(' MathSingletonParamDecl ')' identifier
        '(' MathSingletonParamDecl ')' MathResult {pin=1}

private MathDefinitionParams ::= '(' MathDefinitionParamList ')' { pin=1}
private ParamListRec ::= !(')')
private MathDefinitionParamList ::= MathVarDeclGroup (',' MathVarDeclGroup)* {pin=1 recoverWhile=ParamListRec}

MathSingletonParamDecl ::= MathVarDef ':' MathTypeExp {pin=2 extends=MathVarDeclGroup}

MathVarDeclGroup ::= MathVarDeclList ':' MathTypeExp {pin=1}
private MathVarDeclList ::= MathVarDef &(!(')')) (',' MathVarDef)* {pin=1}
MathVarDef ::= identifier

MathResult ::= ':' MathTypeExp

MathTypeExp ::= MathExp

MathExp ::=  MathPrefixApplyExp|MathPrimary

MathPrefixApplyExp ::=
    MathExp '(' MathExp (',' MathExp)* ')'

private MathPrimary ::=
          MathSymbolExp
        | MathLiteralExp

/*fake MathBinaryExp ::= MathExp+ {
  methods=[left="MathExp[0]" right="MathExp[1]"]
}*/

MathSymbolExp ::= MathReferenceExp MathQualifiedReferenceExp? {elementType=MathReferenceExp}

left MathQualifiedReferenceExp ::= '::' identifier {elementType=MathReferenceExp}

MathReferenceExp::= identifier {methods=[getReference getQualifier] extends=MathExp}

MathLiteralExp ::= (int|'true'|'false')